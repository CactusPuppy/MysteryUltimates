// settings {
//   main {
//     Mode Name: "Mystery Ultimates Dominoes"
//     Description: "Every time you use your ult, you get a random ult! On top of that, any player that uses their ult triggers a domino effect, causing all other players to use their ults as well!"
//   }

//   modes {
//     Control {
//       Competitive Rules: On
//       Limit Roles: 1 Tank 2 Offense 2 Support
//     }

//     Escort {
//       Competitive Rules: On
//       Limit Roles: 1 Tank 2 Offense 2 Support
//     }

//     Flashpoint {
//       Competitive Rules: On
//       Limit Roles: 1 Tank 2 Offense 2 Support
//     }

//     Hybrid {
//       Competitive Rules: On
//       Limit Roles: 1 Tank 2 Offense 2 Support
//     }

//     Push {
//       Competitive Rules: On
//       Limit Roles: 1 Tank 2 Offense 2 Support
//     }
//   }

//   lobby {
//     Max Team 1 Players: 5
//     Max Team 2 Players: 5
//   }
// }

import "main.ostw";

globalvar Number dominoesRadius = WorkshopSettingInteger("ºDominoes Variation", "Domino Radius", 20, 1, 200, 0);
globalvar Boolean dominoesAutoFillUltimate = WorkshopSettingToggle("ºDominoes Variation", "Domino Auto Fill Ultimate", true, 0);
globalvar Number dominoesMinDelay = WorkshopSettingReal("ºDominoes Variation", "Domino Min Delay", 1.5, 0, 30, 10);
globalvar Number dominoesMaxDelay = WorkshopSettingReal("ºDominoes Variation", "Domino Max Delay", 5, 0, 30, 20);
globalvar Boolean dominoTriggersAfterDeath = WorkshopSettingToggle("ºDominoes Variation", "Domino Triggers After Death", true, 30);
globalvar Number initialDominoesCooldown = WorkshopSettingInteger("ºDominoes Variation", "Player Domino Cooldown", 10, 1, 30, 100);

playervar Number dominoesCooldown;
playervar Boolean triggerDomino;
playervar Number dominoesDelay;

rule: "Set variation - Dominaoes"
if (ACTIVE_VARIATION == MUVariation.CLASSIC)
{
  ACTIVE_VARIATION = MUVariation.DOMINOES;
}

rule: "Global credit HUD"
{
  CreateHudText(
    AllPlayers(),
    Header: " Code: KNT1A ",
    Subheader: <"<0> Mystery Ult Dominoes", IconString(Icon.QuestionMark)>,
    Text: "By CactusPuppy",
    Location: Location.Right,
    SortOrder: -10,
    HeaderColor: Color.Orange,
    SubheaderColor: Color.White,
    TextColor: CustomColor(255, 255, 255, 128),
    Reevaluation: HudTextRev.VisibleTo,
    Spectators: Spectators.VisibleAlways
  );
  if (DEBUG_MODE) {
    CreateHudText(
      AllPlayers(),
      Subheader: "DEBUG MODE ENABLED",
      Location: Location.Right,
      SortOrder: -9,
      SubheaderColor: rainbow,
      Reevaluation: HudTextRev.VisibleToAndColor
    );
  }
}

rule: "If player is flagged to trigger a domino, send the appropriate events to the main script"
Event.OngoingPlayer
if (triggerDomino)
{
  LogToInspector(<"Attempting to trigger domino for <0>", EventPlayer()>);
  triggerDomino = false;

  if (ultimateState != UltimateState.NORMAL) {
    LogToInspector(<"Player <0> is not in the NORMAL state (<1>)", EventPlayer(), ultimateState>);
    return;
  }

  if (dominoesCooldown > 0) {
    LogToInspector(<"Domino on cooldown (<1>) for <0>", EventPlayer(), dominoesCooldown>);
    return;
  }

  async! TriggerDomino();
}

void TriggerDomino() playervar "[SUB] Process domino trigger"
{
  BigMessage(EventPlayer(), "Domino Triggered! Prepare to ult...");
  # Wait for a random delay between the min and max delay
  dominoesDelay = RandomReal(dominoesMinDelay, dominoesMaxDelay);
  LogToInspector(<"Waiting <0> seconds to trigger domino for <1>", dominoesDelay, EventPlayer()>);
  ultimatePreparedPlayers.ModAppend(EventPlayer());
  ProcessNewPriorityList();


  if (!dominoTriggersAfterDeath) {
    WaitUntil(IsDead(), dominoesDelay);
    AbortIf(IsDead());
  } else {
    Wait(dominoesDelay);
  }

  if (!dominoesAutoFillUltimate && UltimateChargePercent() != 100) {
    LogToInspector(<"Player <0> does not have full ultimate charge", EventPlayer()>);
    ultimatePreparedPlayers.ModRemoveByValue(EventPlayer());
    ProcessNewPriorityList();
    return;
  }


  LogToInspector(<"Triggering domino for <0>", EventPlayer()>);
  DispatchPreparingUltEvent(PreparingUltEvent.STATE_ENTER);
  DispatchUltimateEvent(UltimateEvent.ULT_ACTIVATION);

  WaitUntil(ultimateState != UltimateState.NORMAL, 1);

  if (placeableUltimateHeroes.Contains(HeroOf())) async SimulateUltButtonPress();
}

void SimulateUltButtonPress() playervar "[SUB]: Stop holding ultimate button delayed"
{
  Wait(0.1);
  StartHoldingButton(EventPlayer(), Button.Ultimate);
  Wait(0.25);
  StopHoldingButton(EventPlayer(), Button.Ultimate);
}

rule: "When a player moves out of the NORMAL state, flag all players nearby to trigger a domino"
Event.OngoingPlayer
if (ultimateState == UltimateState.NORMAL)
{
  WaitUntil([UltimateState.USING_ULT, UltimateState.LINGERING].Contains(ultimateState), 1000000);
  LogToInspector($"{EventPlayer()}-|-{ultimateState}");
  Player[] dominoTargets = PlayersWithinRadius(EventPlayer().Position(), dominoesRadius, Team.All, RadiusLOS.Off).Remove(EventPlayer()).FilteredArray((p) => p.IsAlive());
  dominoTargets.triggerDomino = true;

  WaitUntil(ultimateState == UltimateState.NORMAL, 1000000);
  dominoesCooldown = initialDominoesCooldown;
  ChaseVariableOverTime(dominoesCooldown, 0, initialDominoesCooldown, TimeChaseReevaluation.None);
  Loop();
}

rule: "[DEBUG] Show cooldown"
Event.OngoingPlayer
if (DEBUG_MODE)
{
  CreateHudText(
    EventPlayer(),
    ultimateState,
    <"dominoCooldown: <0>", dominoesCooldown>,
    <"delay: <0>", dominoesDelay>,
    HeaderColor: Color.Orange,
    Location: Location.Left,
    SortOrder: 0,
    Reevaluation: HudTextRev.VisibleToAndString);
}
