// settings {
//   main {
//     Mode Name: "Mystery Heroes + Mystery Ultimates"
//     Description: "Mystery Heroes, but your ultimate is also random!"
//   }

//   modes {
//     General {
//       Limit Roles: Off
//       Tank Role Passive Health Bonus: Disabled
//       Respawn as Random Hero: Enabled
//     }

//     Control {
//       Competitive Rules: On
//     }

//     Escort {
//       Competitive Rules: On
//     }

//     Flashpoint {
//       Competitive Rules: On
//     }

//     Hybrid {
//       Competitive Rules: On
//     }

//     Push {
//       Competitive Rules: On
//     }
//   }
// }
import "OSTWUtils/UltimateChargeConversion.del";
globalvar Boolean RETAIN_ULT_CHARGE = WorkshopSettingToggle("Mystery Heroes + Ultimate", "Retain Ultimate Charge", true);
globalvar Boolean FORCE_MYSTERY_HERO = WorkshopSettingToggle("Mystery Heroes + Ultimate", "Force Mystery Hero on Spawn", false, 1);
globalvar Hero MYSTERY_HERO_TO_FORCE = WorkshopSettingHero("Mystery Heroes + Ultimate", "Hero to Force", Hero.Echo, 2);

playervar Boolean HasForcedBefore;

import "main.ostw";
rule: "Set variation - Mystery Heroes + Mystery Ultimates"
if (ACTIVE_VARIATION == MUVariation.CLASSIC)
{
  ACTIVE_VARIATION = MUVariation.MYSTERY_EVERYTHING;
}

rule: "Global credit HUD"
{
  CreateHudText(
    AllPlayers(),
    Header: " Code: DH07E ",
    Subheader: <"<0> Mys. Heroes + Mys. Ultimate", IconString(Icon.QuestionMark)>,
    Text: "By CactusPuppy",
    Location: Location.Right,
    SortOrder: -10,
    HeaderColor: Color.Orange,
    SubheaderColor: Color.White,
    TextColor: CustomColor(255, 255, 255, 128),
    Reevaluation: HudTextRev.VisibleTo,
    Spectators: Spectators.VisibleAlways
  );
  if (DEBUG_MODE) {
    CreateHudText(
      AllPlayers(),
      Subheader: "DEBUG MODE ENABLED",
      Location: Location.Right,
      SortOrder: -9,
      SubheaderColor: rainbow,
      Reevaluation: HudTextRev.VisibleToAndColor
    );
  }
}

rule: "Select new hero after waiting for players"
Event.OngoingPlayer
if (IsWaitingForPlayers())
{
  WaitUntil(!IsWaitingForPlayers(), 1000000);
  ChooseNewMainHero();
}

rule: "Skip assembling heroes"
if (IsAssemblingHeroes())
{
  SetMatchTime(0.25);
}

rule: "In setup, re-roll player heroes"
Event.OngoingPlayer
if (IsInSetup() || IsWaitingForPlayers())
{
  ChooseNewMainHero();
}

rule: "If player dies and respawns close to a spawn point, change their hero"
Event.OnDeath
{
  WaitUntil(IsAlive(), 1000000);
  Wait(0.064);
  AbortIf(Not(
    SpawnPoints(TeamOf()).Map((p) => PositionOf(<Player>p)).IsTrueForAny((p) => DistanceBetween(EventPlayer(), p) < 1)
  ));

  # If player killed themselves, don't change their hero and remove their ult charge
  if (Attacker() == EventPlayer()) {
    SetUltimateCharge(EventPlayer(), 0);
    return;
  }

  ChooseNewMainHero();
}

playervar Number ultChargeStore;

void ChooseNewMainHero() playervar "[SUB] Choose a new main hero for the player"
{
  Hero[] allowedHeroSwaps = [];

  # If player is a tank, or there's less than 3 players on their team, allow them to swap to a tank
  if (AllTankHeroes().Contains(EventPlayer().Hero())
  || AllPlayers(TeamOf()).FilteredArray((p) => AllTankHeroes().Contains(p.Hero())).Length < 3) {
    allowedHeroSwaps.ModAppend(AllTankHeroes());
  }

  # If player is a support, or there's less than 3 players on their team, allow them to swap to a support
  if (AllSupportHeroes().Contains(EventPlayer().Hero())
  || AllPlayers(TeamOf()).FilteredArray((p) => AllSupportHeroes().Contains(p.Hero())).Length < 3) {
    allowedHeroSwaps.ModAppend(AllSupportHeroes());
  }

  # If player is a damage, or there's less than 3 players on their team, allow them to swap to a damage
  if (AllDamageHeroes().Contains(EventPlayer().Hero())
  || AllPlayers(TeamOf()).FilteredArray((p) => AllDamageHeroes().Contains(p.Hero())).Length < 3) {
    allowedHeroSwaps.ModAppend(AllDamageHeroes());
  }

  # Don't let the player swap to the same hero they were playing
  allowedHeroSwaps.ModRemoveByValue(EventPlayer().Hero());

  trueHero = allowedHeroSwaps.Random();
  if (HeroOf() == Hero.Dva) WaitUntil(UltimateChargePercent() > 0, 0.25);
  ultChargeStore = UltimateChargeConversion.UltimatePoints(HeroOf(EventPlayer()), UltimateChargePercent(EventPlayer()));
  if (DEBUG_MODE && FORCE_MYSTERY_HERO && !HasForcedBefore) {
    trueHero = MYSTERY_HERO_TO_FORCE;
    HasForcedBefore = true;
  }
  ForcePlayerHero(EventPlayer(), trueHero);
  async RestoreUltCharge();
}

void RestoreUltCharge() playervar "[SUB] Restore ultimate charge"
{
  WaitUntil(UltimateChargePercent() > 0 || ultChargeStore == 0, 1000000);
  SetUltimateCharge(EventPlayer(), Min(UltimateChargeConversion.ChargePercentFromPoints(HeroOf(), ultChargeStore), RETAIN_ULT_CHARGE ? 100 : 15));
}
